#!/usr/bin/env nu

# This is a simple script to configure the environment for the
# i3 window manager. It is intended to be used with my configuration
# the general idea of this script is so that our i3 configuration
# binds to "concepts" rather than "commands"

def die [ message: string ] {
    let msg = { msg: $message }
    notify "error" $message
    error make --unspanned $msg
}

def notify [title: string, message: string] {
    notify-send $title $message
}

export def --wrapped "main terminal" [--title: string, ...cmd] {
    mut args = []
    if not ($title | is-empty) { $args = [...$args, $"--title=($title)"] }
    if not ($cmd | is-empty) { $args = [...$args, "-e", ...$cmd] }
    ghostty ...$args
}
export def "main app-launcher" [] { rofi -show drun }
export def "main browser" [] { flatpak run app.zen_browser.zen }
export def "main webapp" [url: string] { google-chrome-stable --app=($url) }
export def "main clipboard" [] { with-env { CM_LAUNCHER: "rofi" } { clipmenu } }
export def "main screenshot" [] {
    scrot -s -e 'mv $f ~/Pictures/Screenshots && xclip -selection clipboard -t image/png -i ~/Pictures/Screenshots/$n'
    notify "Screenshot" 'successfully saved to ~/Pictures/Screenshots'
}

export def "main picker" [
    prompt: string
    --no-custom
] {
    mut args = []
    if not ($prompt | is-empty) { $args = [...$args, "-p", $prompt] }
    if $no_custom { $args = [...$args, "-no-custom"] }
    rofi -matching fuzzy -dmenu -i ...$args
}

export def "main text-input" [
    prompt: string
] {
    mut args = []
    if not ($prompt | is-empty) { $args = [...$args, "-p", $prompt] }
    rofi -dmenu -theme-str 'listview { enabled: false; }' ...$args
}


export def --wrapped main [subcmd: string, ...args] {
    match $subcmd {
        _ => { die $"unknown subcommand: '($subcmd)'"; return; }
    }
}
